# plot style
set ylabel "Survival fraction"
set xlabel "Rate [kHz]"
# set title "Survival fraction vs Bandwidth and Rate: LNL 11 GCU 33 CH data"
set key opaque bottom left
set size 0.8,0.8
set size square
set logscale x

# define tics
set mxtics 10
set mytics 2
set tics textcolor rgb "black"

# define axes
set style line 11 lc rgb '#000000' lt 1
set border ls 11

# define second axis
set xtics nomirror
set x2tics
set autoscale xfix
set autoscale x2fix
set x2label "Bandwidth [Mb/s]"
set mx2tics 10
set logscale x2


# define grid
set style line 12 lc rgb '#808080' lt 0 lw 1
set grid back ls 12

# labels
set label "CH readout: 3 processes per GCU" at graph 0.03,0.16 font ",12"
set label "Single channel analysis" at graph 0.03,0.21 font ",12"
set label "Trigger window: 120" at graph 0.03, 0.26 font ",12"
set label "LNL 11 GCU 33 CH data" at graph 0.03, 0.31 font ",12"

# set label "{/Symbol \273} 460 Mb/s" at graph 0.5, 0.85 font ",13"
# set label "{/Symbol \273} 30 kHz" at graph 0.52, 0.8 font ",13"

# plot con retta verticale
plot "rate_OLD_20210622_CH2048_win120_33ch.dat" u ($1/1000):35 w lp pt 7 ps 1.1 lc rgb "#b20000" t "Old Server", \
"rate_NEW_20210622_CH2048_win120_33ch.dat" u ($1/1000):($2/100) w lp pt 3 ps 1.1 dt 2 linecolor rgb "#397f3f" title "New server", \
"rate_NEW_20210622_CH2048_win120_33ch.dat" u ($1*120*8*16*33/1000000):(NaN) axes x2y1 notitle, \
"rate_OLD_20210622_CH2048_win120_33ch.dat" u ($1*120*8*16*33/1000000):(NaN) axes x2y1 notitle

# set terminal postscript color enhanced
# set output "<name>.eps"
# replot

# set terminal pngcairo size 600,600 enhanced font 'Verdana,10'
# set output "<name>.eps"
# replot


