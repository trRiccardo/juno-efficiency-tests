# plot style
set ylabel "Survival fraction (no channels lost)"
set xlabel "Rate [kHz]"
# set title "Survival fraction vs Bandwidth and Rate: 1 GCU 3 CH data"
set key opaque bottom left
set size 0.8,0.8
set size square
set logscale x

# define tics
set mxtics 10
set mytics 2
set tics textcolor rgb "black"

# define axes
set style line 11 lc rgb '#000000' lt 1
set border ls 11

# define second axis
set xtics nomirror
set x2tics
set autoscale xfix
set autoscale x2fix
set x2label "Bandwidth [Mb/s]"
set mx2tics 10
set logscale x2


# define grid
set style line 12 lc rgb '#808080' lt 0 lw 1
set grid back ls 12

# labels
set label "CH readout: 3 processes per GCU" at graph 0.03,0.16 font ",12"
set label "Data analysis with event building" at graph 0.03,0.21 font ",12"
set label "Buffer length set to 2048" at graph 0.03,0.26 font ",12"
set label "Trigger window: 40" at graph 0.03, 0.31 font ",12"
set label "1 GCU 3 CH data" at graph 0.03, 0.36 font ",12"

# set arrow from graph 0.6, 0.8 rto graph 0.1,0.12 lw 1

set label "{/Symbol \273} 460 Mb/s" at graph 0.5, 0.85 font ",13"
set label "{/Symbol \273} 30 kHz" at graph 0.52, 0.8 font ",13"

# plot con freccia
# plot "evB_IHEP_CH2048_3ch.dat" u ($1/1000):3 w lp pt 4 ps 1 linecolor rgb "#b20000" title "IHEP", \
# "evB_LNL_CH2048_3ch.dat" u ($1/1000):3 w lp pt 7 ps 1 linecolor rgb "#397f3f" title "LNL", \
# "evB_IHEP_CH2048_3ch.dat" u ($1*40*8*16*3/1000000):(NaN) axes x2y1 notitle, \
# "evB_LNL_CH2048_3ch.dat" u ($1*40*8*16*3/1000000):(NaN) axes x2y1 notitle

# plot con retta verticale
plot "barriera.txt" u ($1*40*8*16*3/1000000):2 with impulses dt 2 lw 1.5 linecolor rgb "#0" axes x2y1 notitle, \
"evB_IHEP_CH2048_3ch.dat" u ($1/1000):3 w lp pt 4 ps 1 linecolor rgb "#b20000" title "IHEP", \
"evB_LNL_CH2048_3ch.dat" u ($1/1000):3 w lp pt 7 ps 1 linecolor rgb "#397f3f" title "LNL", \
"evB_IHEP_CH2048_3ch.dat" u ($1*40*8*16*3/1000000):(NaN) axes x2y1 notitle, \
"evB_LNL_CH2048_3ch.dat" u ($1*40*8*16*3/1000000):(NaN) axes x2y1 notitle



# set terminal postscript color enhanced
# set output "<name>.eps"
# replot

# set terminal pngcairo size 600,600 enhanced font 'Verdana,10'
# set output "<name>.eps"
# replot


