# plot style
set ylabel "Survival fraction"
set xlabel "Rate [kHz]"
set key opaque bottom left
set size 0.8,0.8
set size square
set logscale x

# define tics
set mxtics 10
set mytics 2
set tics textcolor rgb "black"

# define axes
set style line 11 lc rgb '#000000' lt 1
set border ls 11

# define second axis
set xtics nomirror
set x2tics
set autoscale xfix
set autoscale x2fix
set x2label "Bandwidth [Mb/s]"
set mx2tics 10
set logscale x2


# define grid
set style line 12 lc rgb '#808080' lt 0 lw 1
set grid back ls 12

# labels
set label "Single channel analysis" at graph 0.03,0.21 font ",12"
set label "Trigger window: 40" at graph 0.03, 0.26 font ",12"
set label "LNL 11 GCU 33 CH data" at graph 0.03, 0.31 font ",12"

# set arrow from graph 0.6, 0.8 rto graph 0.1,0.12 lw 1

# set label "{/Symbol \273} 460 Mb/s" at graph 0.5, 0.85 font ",13"
# set label "{/Symbol \273} 30 kHz" at graph 0.52, 0.8 font ",13"
# set arrow from 30, graph 0 to 30, graph 1 nohead dt 2 lc rgb "#0"

plot "sCh_LNL_CH2048_33ch.txt" u ($1/1000):($2/100) w lp pt 7 ps 1.1 linecolor rgb "#b20000" title "CH - 2048", \
"sCh_LNL_GCU2048_33ch.txt" u ($1/1000):($2/100) w lp pt 4 ps 1.1 dt 2 linecolor rgb "#397f3f" title "GCU - 2048", \
"sCh_LNL_GCU_2048-512_33ch.txt" u ($1/1000):($2/100) w lp pt 3 ps 1.1 dt 4 linecolor rgb "#0066cc" title "GCU - 512", \
"sCh_LNL_CH2048_33ch.txt" u ($1*40*8*16*33/1000000):(NaN) axes x2y1 notitle, \
"sCh_LNL_GCU2048_33ch.txt" u ($1*40*8*16*33/1000000):(NaN) axes x2y1 notitle, \
"sCh_LNL_GCU_2048-512_33ch.txt" u ($1*40*8*16*33/1000000):(NaN) axes x2y1 notitle

# set terminal postscript color enhanced
# set output "<name>.eps"
# replot

# set terminal pngcairo size 600,600 enhanced font 'Verdana,10'
# set output "<name>.eps"
# replot


